Command 				::= 	single-Command (";" single-Command)*

single-Command 			::=		"pass"
						|		V-name ":=" Expression
						|		Long-Identifier ( Actual-Parameter-Sequence )
						|		Loop-Command
						|		"let" Declaration "in" Command "end"
						|		"if" Expression "then" Command "else" Command "end"
						|		"choose" Expression "from" Cases "end"

Loop- Command			::= 	"loop" (For-Command | Looper) "end" 

For-Command			::=		"for" Identifier "from" Expression "to" Expression [ W-or-U Expression] Action

Action					::= 	"do" Command

Looper 					::= 	W-or-U "Expression" Action
						|		Action W-or-U "Expression"

W-or-U					::= 	"While" | "Until"

Cases 					::= 	Case (Case)* [ ElseCase ]

Case					::=		"when" Case-Literals "then" Command

ElseCase				::= 	"else" Command

Case-Literals			::= 	Case-Range ( "|" Case-Range)*

Case-Range				::= 	Case-Literal [ ".."  Case-Literal ]

Case-Literal 			::= 	Integer-Literal	
						|		Character-Literal

Declaration 			::=		compound-Declaration (";"  compound-Declaration)*

compound-Declaration	::=		single-Declaration
						| 		"recursive" Proc-Funcs "end"
						| 		"private" Declaration "in" Declaration "end"
						| 		"par" single-Declaration "|" single-Declaration ("|" single-Declaration)* "end"

Proc-Func				::= 	"proc" Identifier "(" Formal-Parameter-Sequence ")"
									"~" Command "end"
						| 		"func" Identifier "(" Formal-Parameter-Sequence ")"
									":" Type-denoter "~" Expression

Proc-Funcs				::= 	Proc-Func "|" Proc-Func ("|" Proc-Func)*

single-Declaration		::= 	"const" Identifier "~" Expression
						|		"var" Identifier ":" Type-denoter
						|		"var" Identifier "::=" Expression
						|		"proc" Identifier "(" Formal-Parameter-Sequence ")" 
									"~" Command "end"
						|		"func" Identifier "(" Formal-Parameter-Sequence  ")"
									":" Type-denoter "~" Expression
						|		"type" Identifier "~" Type-denoter

Program					::=		( Package-Declaration ";" )* Command

Package-Declaration		::= 	"package" Package-Identifier "~" Declaration "end"

Package-Identifier 		::= 	Identifier

Long-Identifier 		::= 	[ Package-Identifier "$" ] Identifier

V-name					::= 	[ Package-Identifier "$" ] Var-Name

Var-Name				::=		Identifier
						|		Var-Name "." Identifier
						|		Var-Name "[" Expression "]"

Type-denoter			::=		Long-Identifier
						|		"array" Integer-Literal of Type-denoter
						|		"record" Record-Type-denoter "end"